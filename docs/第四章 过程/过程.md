##过程   
如果你想要定义一个像echo和readLine这样的命令怎么去实现呢，这里就需要引入一个感念：过程。在其他的语言中把它称为方法或者函数。在Nim中定义一个过程要用关键字proc，下面就让我们看看怎么定义一个过程。

    proc yes(question: string): bool =
      echo(question, " (y/n)")
      while true:
        case readLine(stdin)
        of "y", "Y", "yes", "Yes": return true
        of "n", "N", "no", "No": return false
        else: echo("Please be clear: yes or no")
    
    if yes("Should I delete all your important files?"):
      echo("I'm sorry Dave, I'm afraid I can't do that.")
    else:
      echo("I think you know what the problem is just as well as I do.")

这个例子中定义了一个名为yes的过程，其参数为question，参数类型为string，返回值为布尔类型。下面的if语句调用了yes过程，传入字符串参数，过程yes的功能是询问一个问题，等待你的输入，y/n会返回相应的true或false。在过程中可以用return语句来实现返回一个值。bool是Nim中的内建类型，即布尔类型，布尔类型仅仅有两个有效值true和false，if语句和while语句中的条件就是bool类型。

在这个例子中question称为形式参数，简称形参。"Should I ....."称为实际参数，简称实参。在程序运行过程中，实际参数就会见参数值传递给相应的形式参数。

当然你的过程也可以没有返回值，一起来看下面的例子：

    proc Hi(name: string) = 
      echo "Hello,", name
    
    Hi("yrs")



###过程数据类型
我们也可以定义一个类型是过程数据类型。例如：

    type
      TCallback = proc (a, b: int)
    
    proc huh(x, y: int) =
      echo x
      echo y
    
    proc so(c: TCallback) =
      c(2, 4)
    
    so(huh)

#

    proc pa() {.cdecl.} = write(stdout, "pa")
    proc pb() {.cdecl.} = write(stdout, "pb")
    proc pc() {.cdecl.} = write(stdout, "pc")
    proc pd() {.cdecl.} = write(stdout, "pd")
    proc pe() {.cdecl.} = write(stdout, "pe")
    
    const
      algos = [pa, pb, pc, pd, pe]
    
    var
      x: proc (a, b: int): int {.cdecl.}
    
    proc ha(c, d: int): int {.cdecl.} =
      echo(c + d)
      result = c + d
    
    for a in items(algos):
      a()
    
    x = ha
    discard x(3, 4)
    
    #OUT papbpcpdpe7


#

    proc doSomething(v: int, x: proc(v:int):int): int = return x(v)
    proc doSomething(v: int, x: proc(v:int)) = x(v)
    
    echo doSomething(10, proc(v: int): int = return v div 2)
