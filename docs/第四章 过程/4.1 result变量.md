##4.1 result变量
在Nim中，每一个定义的有返回值的过程中都有一个隐式的result变量被声明，result代表的是返回值。一个没有表达式的return语句表示的是return result。如果在过程中没有return语句，那么在过程的结尾会自动返回result值。

    proc sumTillNegative(x: varargs[int]): int =
      for i in x:
        if i < 0:
          return
        result = result + i
    
    echo sumTillNegative() # echos 0
    echo sumTillNegative(3, 4, 5) # echos 12
    echo sumTillNegative(3, 4 , -1 , 6) # echos 7

该程序中形参的类型是不定参数，不定参数在高级数据类型中会详细讲解，这里简单的了解一下就好，就像该程序调用sumTillNegative过程似的，能够传入不同数量的参数。

result变量在有返回值的过程中是默认被声明的，在没有返回值的过程中没有默认声明result。Result变量的类型与返回值的类型相同，因为它就代表返回值。Result的初始值是所属类型的初始值，像整型的话，result的初始值为0。需要注意的是，对于引用数据类型，result的初始值将会是nil，因此在使用result时可能需要手动初始化，例如string类型。

返回类型为int类型：

    proc sum(a, b: int): int = 
      echo result
      result = a + b
      #没有return语句，自动返回result值
    
    echo sum(1,2)
    #会输出 0 换行 3

返回类型为string类型：

    proc linkStr(x: varargs[string]): string = 
      echo result   #输出为 nil
      result = ""  #注意要初始化result，不然会出现编译错误
      for sub in x:
        result = result & sub #运算符& 链接两个字符串
    
    echo linkStr("abc","def","ghi")


既然result已经隐式的被声明，如果我们在过程里在重新声明一个result变量将会出现什么情况呢？下面就让我们来看看：

    proc sum(a, b: int): int =
      var result: int
      result = a + b
    
    echo sum(1,2)   #echo  0

这个程序会输出0，显然返回的不是过程中重新声明的result，而是默认声明的result。由此可以看出，如果在过程中重新声明了变量result，那么它将与默认声明的result共存，只不过是一个普通的变量。如果没有return语句，那么该过程自动返回的是默认声明的result。如果明确的给出return result语句，那么将会返回重新声明的result变量。如下面这个程序：

    proc sum(a, b: int): int =
      var result: int
      result = a + b
      return result 
    
    echo sum(1,2)   #echo  3



Nim为程序员提供了非常自由的调用过程方式，对于一个过程foo(a, b)，我们条用它的方式有：foo(a,b),  a.foo(b),  a.foo b。下面我们来看一个计算斐波那契数的例子：

    proc fibonacci(n: int): int =  
      if n < 2:
        result = n  
      else:
        result = fibonacci(n - 1) + (n - 2).fibonacci
    
    echo fibonacci(10)
    echo 10.fibonacci()
    echo fibonacci 10



当然你也可以定义一个运算符，例如：

    proc `^^`(a, b: int): int = 
      #这个运算符`^^`表示a 的 b 次方,要求b为非负数 
      var n = b
      if b < 0:
        return
      elif b == 0:
        result = 1
      else:
        result = a
      while n>1:
        result = result * a
        dec(n)
    
    echo 3 ^^ 3
    echo `^^`(2,4)
  
这个例子中，我们定义了一个运算符`^^`，注意定义运算符要使用反引号包裹起来，使用运算符时可以像使用内置的运算符一样，也可以像使用过程的方式，如上例所示，其他的和定义一个普通的过程没有什么区别。