##面向对象编程

###编译指示
编译指示是Nim给编译器附加信息的方法/ 命令不引入大量的新关键词。编译指示是特别的用大括号和点标识的，如： {.and.}。 这个教程不包括编译指示。看手册（Manual）或用户指南( user guide)描述了可用的编译指示。

###面向对象程序设计
Nim 支持面向对象编程(OOP)是极简单的，并且可以使用强大的OOP技术。OOP被视为一种方法设计一个程序，而非唯一方法。通常一个过程式的方法会导致更简单高效的代码。特别的，优先使用对象组合，而非（类）继承（面向对象的设计法则）通常来说是更好的设计。

####对象
像元组和对象是以一种结构化的方式把不同的值包装在一起。然而对象提供更多元组没有的属性：他们提供继承和信息隐藏。因为对象封装数据，T() 对象构造函数应该仅仅被用在内部，程序员应该提供一个过程用来初始化对象（这叫做构造器）
在运行时对象可以访问它们的类型，有一个 of 操作符，能够用来检查对象的类型。

```
type
  Person = ref object of RootObj
    name*: string  # *号意味着可以在其他模块使用name变量
    age: int       # 没有*号意味着其对于其他模块是隐藏的
  
  Student = ref object of Person # Student 继承 Person
    id: int                      # Student 有一个id 域变量

var
  student: Student
  person: Person
assert(student of Student) # is true
# 对象构造:
student = Student(name: "Anton", age: 5, id: 2)
echo student[]
```

对象域对外面是可见的，模块不得不用* 号标记。与元组相比，不同对象类型是从不会相等的。新的对象类型只能被定义在 type 部分内。

继承是用 object of 语法。现在还不支持多继承。如果一个对象类型没有合适的祖先，RootObj可以作为它的祖先，但 这只是一个约定。没有祖先的对象是隐藏的final(???)。你可以用inheritable编译指示来产生一个除了来自system.RotObj之外的的根对 象。（例如：这被用在GTK包）。 

每当使用继承时应使用ref对象。它不是绝对必要的，但是用非ref对象赋值，如：let person: Person = Student(id: 123)将截断子类域。

杨注：如果使用非ref 对象，用person：Person = Student(name:”mm”,age:22,id:123) 赋值语句，会编译出错，类型不对。 如果用ref 对象，会截断id 域。echo person  为（name:”mm”, age: 5)。与上面说的不同。

注意：组合(has-a 关系)往往优于继承（is-a 关系）对于简单的代码重用。由于在nim中对象是一种值类型，组合和继承一样有高效。注：（引用类型（重量级对象）和值类型（轻量级对象））

####相互递归类型
对象，元组和引用可以塑造相当复杂的数据结构相互依赖彼此；它们是相互递归。在nim中这些类型只能在一个单一的 type 部分声明。（其他任何需要任意前端符号会减慢编辑。）

例如：
```
type
  Node = ref NodeObj # 一个NodeObj的跟踪引用。
  NodeObj = object
    le, ri: Node     # left and right subtrees  左 和 右子树
    sym: ref Sym     # 叶子节点包含一个Sym 的引用
  
  Sym = object       # a symbol    一个符号
    name: string     # the symbol's name  符号的名字
    line: int        # the line the symbol was declared in
    code: Node      # the symbol's abstract syntax tree  符号的抽象语法树
```

####类型转换

nim区分显示的类型转换和隐式的类型。显示的类型转换用casts操作符并且强制编译器解释一种位模式成为另一种类型。

隐式的类型转换是一个更礼貌的方式将一个类型型转换为另一个：他们保存摘要值，不一定是位模式。如果一个类型转换是不可能的，编译器会控诉或者抛出一个异常。

类型转换语法是：destination_type(expression_to_convert)  目的类型（要转换的表达式）（像一个普通的调用）
```
proc getID(x: Person): int =      
  Student(x).id
```

如果x不是一个Student类型，会抛出InvalidObjectConversionError异常。

####对象变体

通常一个对象层次结构在特定的情况下是不必要的，需要简单的变体类型。

一个例子:
```
# This is an example how an abstract syntax tree could be modelled in Nim
type
  NodeKind = enum  # the different node types
    nkInt,          # a leaf with an integer value
    nkFloat,        # a leaf with a float value
    nkString,       # a leaf with a string value
    nkAdd,          # an addition
    nkSub,          # a subtraction
    nkIf            # an if statement
  Node = ref NodeObj
  NodeObj = object
    case kind: NodeKind  # the ``kind`` field is the discriminator
    of nkInt: intVal: int
    of nkFloat: floatVal: float
    of nkString: strVal: string
    of nkAdd, nkSub:
      leftOp, rightOp: Node
    of nkIf:
      condition, thenPart, elsePart: Node

var n = Node(kind: nkFloat, floatVal: 1.0)
# the following statement raises an `FieldError` exception, because
# n.kind's value does not fit:
n.strVal = ""
```

可以从这个例子中看到，一个对象层次结构的一个优点是，不需要不同的对象类型之间的转换。然而，访问无效的对象域会引发一个异常。

####方法

在普遍的面向对象程序设计语言中，过程（也叫做方法）被绑定到一个类。这种做法有缺点：


- ·程序员无法控制添加一个方法到一个类中是不可能的或者需要丑陋的解决方法。


- ·很多情况下方法应该属于哪里是不清楚的：是加入一个字符串方法还是一个数组方法？

nim通过不分配方法到一个类中避免了这样的问题。所有的方法在nim中都是多方法。后面我们将看到，多方法区别与过程只为了动态绑定目的。


####方法调用语法

对于调用例程有一个语法：可以用语法obj.method(args)而不是method(obj,args).如果没有剩余的参数，圆括号可以省略：obj.len(而不是len(obj))。

这个方法调用语法是不受对象限制的，它可以被用于任何类型。
```
import strutils

echo("abc".len) # is the same as echo(len("abc"))
echo("abc".toUpper())
echo({'a', 'b', 'c'}.card)
stdout.writeln("Hallo") # the same as writeln(stdout, "Hallo")
```
（另一种方式来看待方法调用语法是它提供了缺失的后缀表示法.)

所以纯面向对象代码是容易写的：
```
import strutils

stdout.writeln("Give a list of numbers (separated by spaces): ")
stdout.write(stdin.readLine.split.map(parseInt).max.`$`)  #输入几个以空格键为分隔的数字，会#输出最大值。
stdout.writeln(" is the maximum!")

```



####特性
如上面的例子所示，nim没必要get-properities:通常get-procedures被称为方法调用语法实现相同的功能。但是设定一个值是不一样的，这需要一个特殊的setter语法：
```
type
  Socket* = ref object of RootObj
    FHost: int # 在其他的外部模块不能调用 FHost。
               # ‘F’ 前缀是一个避免冲突的表识约定，因为函数的名字叫做 ‘hoat'。
               
proc `host=`*(s: var Socket, value: int) {.inline.} =
  ## setter of hostAddr
  s.FHost = value

proc host*(s: Socket): int {.inline.} =
  ## getter of hostAddr
  s.FHost

var s: Socket
new s
s.host = 34  # same as `host=`(s, 34)
echo s.host()
```

(示例还显示了inline（内联）程序。)

[] 数组访问运算符可以被重载以提供数组属性：
```
type
  Vector* =  object
    x, y, z: float

proc `[]=`* (v: var Vector, i: int, value: float) =
  # setter
  case i
  of 0: v.x = value
  of 1: v.y = value
  of 2: v.z = value
  else: assert(false)

proc `[]`* (v: Vector, i: int): float =
  # getter
  case i
  of 0: result = v.x
  of 1: result = v.y
  of 2: result = v.z
  else: assert(false)

var vector1: Vector
vector1[0] = 1.0
echo vector1[0]
```
   
这个例子是愚蠢的，因为一个向量通过一个元组可以更好的模拟，元组已经提供v[]访问。


####动态分配

程序总是使用静态分配。对于动态分配使用关键字method来代替proc：
```
type
  PExpr = ref object of RootObj ## abstract base class for an expression
  PLiteral = ref object of PExpr
    x: int
  PPlusExpr = ref object of PExpr
    a, b: PExpr

# watch out: 'eval' relies on dynamic binding
method eval(e: PExpr): int =
  # override this base method
  quit "to override!"

method eval(e: PLiteral): int = e.x
method eval(e: PPlusExpr): int = eval(e.a) + eval(e.b)

proc newLit(x: int): PLiteral = PLiteral(x: x)
proc newPlus(a, b: PExpr): PPlusExpr = PPlusExpr(a: a, b: b)

echo eval(newPlus(newPlus(newLit(1), newLit(2)), newLit(4)))
```

     
注意：在例子中，构造器newLit和newPlus是过程，因为对于它们使用静态绑定更有意义，但是eval是一个方法因为它需要动态绑定。
     
在一个多方法的所有参数中有一个对象类型用于分配:
```
type
  Thing = ref object of RootObj
  Unit = ref object of Thing
    x: int

method collide(a, b: Thing) {.inline.} =
  quit "to override!"

method collide(a: Thing, b: Unit) {.inline.} =
  echo "1"

method collide(a: Unit, b: Thing) {.inline.} =
  echo "2"

var a, b: Unit
new a
new b
collide(a, b) # output: 2
```
   
如上面那个例子所示，调用一个多方法不能是模棱两可的。相比collide 1，collide 2是首选，因为决议是从左到右工作的。因此，Unit, Thing优于Thing, Unit。

注意：nim不产生虚拟方法表，但是生成调用树。这样为方法调用和使用内联避免了多余的间接分支。然而，其他的优化像：编译时间评估或者无用代码消除对于方法是不起作用的。


###inline
    
内联约定调用者不应该调用这个程序，而是直接内联它的代码。注意，Nim 不能内联，但是离开Nim到C编译器：它生成 __inline 程序。这只是一个提示对于编译器：它可能完全忽视它,它可能会内联程序不标记为内联。